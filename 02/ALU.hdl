// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Mux16(a=x, b=false, sel=zx, out=tmpx1);
    Not16(in=tmpx1, out=notTmpx1);
    Mux16(a=tmpx1, b=notTmpx1, sel=nx, out=tmpx2);
    Mux16(a=y, b=false, sel=zy, out=tmpy1);
    Not16(in=tmpy1, out=notTmpy1);
    Mux16(a=tmpy1, b=notTmpy1, sel=ny, out=tmpy2);
    Add16(a=tmpx2, b=tmpy2, out=addout);
    And16(a=tmpx2, b=tmpy2, out=andout);
    Mux16(a=andout, b=addout, sel=f, out=tmp);
    Not16(in=tmp, out=nottmp);
    Mux16(a=tmp, b=nottmp, sel=no, out=out, out=result);

    And16(a=true, b=result, out[15]=ng, out[0..14]=discard);
    Or16(a=false, b=result, out[0..7]=result1, out[8..15]=result2);
    Or8Way(in=result1, out=outOr1);
    Or8Way(in=result2, out=outOr2);
    Or(a=outOr1, b=outOr2, out=outOr);
    Mux(a=true, b=false, sel=outOr, out=zr);

}